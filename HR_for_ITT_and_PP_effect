global dataset = "C:\Users\micha\Desktop\Presentations\Oslo\Final\"


**************
* QUESTION 1 *
**************

*   check the data (this is the "Initial Dataset")

use "$dataset\Final Practical TTE (causal survival analysis).dta"  , replace

desc

* Calculation of the ITT hazard ratios


**************
* QUESTION 2 *
**************

*   Cox regression 

use "$dataset\Final Practical TTE (causal survival analysis).dta"  , replace

* We will use one observation per subject

by id: egen time_Cox=max(actual_time)
by id: egen Y_Cox=max(Y_t1)

keep if time==0

stset time_Cox,f(Y_Cox)

stcox A male age L


**************
* QUESTION 3 *
**************

* Pooled logistic regression

use "$dataset\Final Practical TTE (causal survival analysis).dta"  , replace

drop actual_time
drop if Y_t1==.

* create baseline variables for A and L
gen A_0=A if time==0
gen L_0=L if time==0

by id: egen A_bas=max(A_0)
by id: egen L_bas=max(L_0)


* confounders adjusted in the outcome model
logistic Y_t1 A_bas male age L_bas time c.time#c.time c.time#c.time#c.time, cluster(id)

**************
* QUESTION 4 *
**************

* confounders adjusted through IPW (unstabilised weight)
logistic A_bas male age L_bas if time==0

predict p
gen     pr=p   if A_bas==1
replace pr=1-p if A_bas==0


gen IPW=1/pr

sum pr IPW
logistic Y_t1 A_bas time c.time#c.time c.time#c.time#c.time [pw=IPW], cluster(id)


save "$dataset\Practical TTE (used for bootstrap).dta" , replace



******************************************************
******************************************************



**************
* QUESTION 6 *
**************

use "$dataset\Practical TTE (used for bootstrap).dta" , replace


* Calculation of the PP hazard ratios

* First, we need to calculate the variable "adherence" for deviation from the initial treatment
* --> for those who did not initiate treatment at baseline (A=0 at baseline)
* --> for those who initiated treatment at baseline (A=1 at baseline) 

gen     adh=1 if A_bas==A
replace adh=0 if A_bas~=A

/*
gen     adh=1 if A_bas==A & A_bas==0
replace adh=0 if A_bas~=A & A_bas==0

replace adh=1 if A_bas==A & A_bas==1
replace adh=0 if A_bas~=A & A_bas==1
*/

tab adh if A_bas==0
tab adh if A_bas==1

* we find the time point that the deviation occurs 
by id: egen cens_visit = min(cond(adh == 0),time, .)


* we create the variable "cens_time" to denote the time of deviation
* this variable will be used later in the analysis stage - only individuals with "cens_time==0" will be analysed

 
gen     cens_time = 0
replace cens_time = 1 if time == cens_visit
replace cens_time = . if time >  cens_visit



* we create the variable "A_pr" to denote A in the previous time point

by id: gen A_pr=A[_n-1]

order id A A_pr adh cens_time

save "$dataset\Practical TTE (analysis adherence).dta" , replace

* First we will calculate the probability of adherence to 
* a) non treamtent and b) treatment
*
* Second, we will then censor the individuals who did not adhere to A=0 and A=1 over time
*
* Third, we will weigh the individuals who actually adhered with the inverse of the probability of adherence to 
* create a pseudo-population where everyone either continuously adheres to treatment or no treatment
*
* Forth, we will estimate the treatment effect from a weighted pooled logistic regression model, 
* strictly in the person-time where individuals are continuously adherent, to get estimates in the pseudo-population.


************************
* Unstabilised weights *
************************


* We calculate the adherence weights for those who did not initiate treatment
* We first calculate the probability of adherence for non treatment initiators


use  "$dataset\Practical TTE (analysis adherence).dta" , replace

**** We calculate the Pr(A/A_pr and baseline confounders and time dependent confounders)

* We run 2 models for time>0

* --> one when A_pr=0
logistic A male age L_bas L time c.time#c.time c.time#c.time#c.time if A_pr==0 & time>0, cluster(id)

predict p_A0
* Attention - here we calculate Pr(A=1/A_pr=0 and baseline confounders and time dependent confounders)


* --> one when A_pr=1
logistic A male age L_bas L time c.time#c.time c.time#c.time#c.time if A_pr==1 & time>0, cluster(id)

predict p_A1
* Attention - here we calculate Pr(A=1/A_pr=1 and baseline confounders and time dependent confounders)



keep if cens_time==0


* we need to calculate the probability of adherence separately for A_pr==0 and A_pr==1
gen     p_A=1-p_A0 if A==0
replace p_A=p_A1   if A==1

* probability of adherence is set to one at baseline
replace p_A=1 if time==0 

by id: replace p_A=p_A*p_A[_n-1] if _n~=1



* We now calculate the IPW for adherence



gen     IPW_A=1/p_A

order id A IPW_A p_A p_A0 p_A1 IPW_A


* or emulate randomisation through outcome regression and weigh the model by IPW_A


logistic Y_t1 A_bas male age L_bas time c.time#c.time c.time#c.time#c.time [pw=IPW_A] if cens_time==0, cluster(id)

* calculate the truncated weights for IPW_A
gen     IPW_A_tr=IPW_A
replace IPW_A_tr=25 if IPW_A>25

logistic Y_t1 A_bas male age L_bas time c.time#c.time c.time#c.time#c.time [pw=IPW_A_tr] if cens_time==0, cluster(id)

save "$dataset\Dataset (used for risk curves).dta" , replace

